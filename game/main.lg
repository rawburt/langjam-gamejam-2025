use std
use tiles
use map
use items

------------------------------ [ make sprites for rendering text ]

const CHARS: [str] = [
  "A", "B", "C", "D", "E", "F", "G", "H",
  "I", "J", "K", "L", "M", "N", "O", "P",
  "Q", "R", "S", "T", "U", "V", "W", "X",
  "Y", "Z",
  ".", ",", ":", "?", "!"
]

def char_index(char: str) int do
  var index: int = 0
  for i = 0 to len(CHARS) do
    if CHARS[i] == char do
      index = i
      break
    end
  end
  ret index
end

var char_sprites: [Sprite] = []

--- "A" - "Z"
for x = 0 to 26 do
  push(make_sprite(x * TILE_SIZE, 48), char_sprites)
end
--- "."
push(make_sprite(104, 40), char_sprites)
--- ","
push(make_sprite(112, 40), char_sprites)
--- ":"
push(make_sprite(120, 40), char_sprites)
--- "?"
push(make_sprite(160, 40), char_sprites)
--- "!"
push(make_sprite(0, 40), char_sprites)

def render_text(text: str, x: int, y: int) do
  for i = 0 to len(text) do
    if text[i] != " " do
      var sprite_index: int = char_index(text[i])
      var sprite: Sprite = char_sprites[sprite_index]
      render_sprite(sprite, x + i * TILE_SIZE, y * TILE_SIZE)
    end
  end
end

------------------------------ [ player ]

rec Player do
  sprite: Sprite
  pos: Vec2
  inventory: [Item]
end

var player: Player = Player(
  sprite: make_sprite(8, 40),
  pos: Vec2(x: 8, y: 12),
  inventory: []
)

------------------------------ [ map ]

def render_tile(sprite: Sprite, x: int, y: int) do
  render_sprite(sprite, x * TILE_SIZE, y * TILE_SIZE)
end

def render_map_tile(tile: int, x: int, y: int) do
  var sprite: Sprite = map_sprite(tile)
  render_tile(sprite, x, y)
end

var camera: Vec2 = Vec2(x: 0, y: 0)

def draw_map() do
  for y = 0 to 16 do
    for x = 0 to 16 do
      --- check map array boundaries
      var map_x: int = x + camera.x
      var map_y: int = y + camera.y
      if map_x >= 0 && map_y >= 0 && map_x < MAP_WIDTH && map_y < MAP_HEIGHT do
        var tile: int = MAP[map_y * MAP_WIDTH + map_x]
        if tile != 0 do
          render_map_tile(tile, x, y)
        end
      end
    end
  end
end

def can_walk(x: int, y: int) bool do
  ret MAP[y * MAP_WIDTH + x] == 0
end

------------------------------ [ item draw ]

var items: [Item] = [ITEM_KEY]

def draw_items() do
  for i = 0 to len(items) do
    if !items[0].picked_up do
      var item_x: int = items[i].pos.x - camera.x
      var item_y: int = items[i].pos.y - camera.y
      if item_x >= 0 && item_y >= 0 && item_x < 16 && item_y < 16 do
        render_tile(items[i].sprite, item_x, item_y)
      end
    end
  end
end

------------------------------ [ other stuff ... ]

enum Scene do
  Main
  Inventory
end

var scene: Scene = Scene.Main

def update_player() do
  --- player movement
  if buttonp(Key.Left) && can_walk(player.pos.x - 1, player.pos.y) do
    player.pos.x -= 1
  end
  if buttonp(Key.Right) && can_walk(player.pos.x + 1, player.pos.y) do
    player.pos.x += 1
  end
  if buttonp(Key.Up) && can_walk(player.pos.x, player.pos.y - 1) do
    player.pos.y -= 1
  end
  if buttonp(Key.Down) && can_walk(player.pos.x, player.pos.y + 1) do
    player.pos.y += 1
  end
  --- scene selection
  if buttonp(Key.X) do
    scene = Scene.Inventory
  end

  --- pick up items
  for i = 0 to len(items) do
    if !items[0].picked_up do
      if items[0].pos.x == player.pos.x && items[0].pos.y == player.pos.y do
        push(items[0], player.inventory)
        items[0].picked_up = true
      end
    end
  end
end

def update_camera() do
  camera.x = player.pos.x - 8
  camera.y = player.pos.y - 8
end
--- update camera before game starts
update_camera()

def draw_player() do
  render_tile(player.sprite, 8, 8)
end

def update_inventory() do
  if buttonp(Key.Z) do
    scene = Scene.Main
  end
end

def draw_inventory() do
  if len(player.inventory) == 0 do
    render_text("EMPTY", 5, 0)
  else
    for i = 0 to len(player.inventory) do
      render_sprite(player.inventory[i].sprite, 0, i)
      render_text(player.inventory[i].detail, 16, i)
    end
  end
end

def update() do
  if scene == Scene.Main do
    update_player()
    update_camera()
  end
  if scene == Scene.Inventory do
    update_inventory()
  end
end

def draw() do
  clear()
  if scene == Scene.Main do
    draw_map()
    draw_player()
    draw_items()
  end
  if scene == Scene.Inventory do
    draw_inventory()
  end
end
