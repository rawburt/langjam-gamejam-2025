use std

asset SPRITE_IMAGE = "assets/classicrogue1.png"

const TILE_TEXTURE: Texture = Texture(
  src: SPRITE_IMAGE,
  w: 224,
  h: 64
)

const TILE_SIZE: int = 8

def make_sprite(x: int, y: int) Sprite do
  ret Sprite(texture: TILE_TEXTURE, rect: Rect(x: x, y: y, w: TILE_SIZE, h: TILE_SIZE))
end

------------------------------ [ make sprites for rendering text ]

const CHARS: [str] = [
  "A", "B", "C", "D", "E", "F", "G", "H",
  "I", "J", "K", "L", "M", "N", "O", "P",
  "Q", "R", "S", "T", "U", "V", "W", "X",
  "Y", "Z",
  ".", ",", ":", "?", "!"
]

def char_index(char: str) int do
  var index: int = 0
  for i = 0 to len(CHARS) do
    if CHARS[i] == char do
      index = i
      break
    end
  end
  ret index
end

var char_sprites: [Sprite] = []

--- "A" - "Z"
for x = 0 to 26 do
  push(make_sprite(x * TILE_SIZE, 48), char_sprites)
end
--- "."
push(make_sprite(104, 40), char_sprites)
--- ","
push(make_sprite(112, 40), char_sprites)
--- ":"
push(make_sprite(112, 40), char_sprites)
--- "?"
push(make_sprite(160, 40), char_sprites)
--- "!"
push(make_sprite(0, 40), char_sprites)

def render_text(text: str, x: int, y: int) do
  for i = 0 to len(text) do
    if text[i] != " " do
      var sprite_index: int = char_index(text[i])
      var sprite: Sprite = char_sprites[sprite_index]
      debug(str(sprite.rect))
      render_sprite(sprite, x + i * TILE_SIZE, y * TILE_SIZE)
    end
  end
end

------------------------------ [ map and camera ]

var start_pos: Vec2 = Vec2(x: 1, y: 1)

--- 0 = ground
--- 1 = wall
--- 2 = water

const MAP: [[int]] = [
  [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1],
  [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
  [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
  [1,0,0,0,0,0,2,2,2,2,0,0,0,0,0,1],
  [1,0,0,0,0,2,2,2,2,2,0,0,0,0,0,1],
  [1,0,0,0,0,2,0,0,0,0,0,0,0,0,0,1],
  [1,0,0,0,0,2,2,0,0,0,0,0,0,0,0,1],
  [1,0,0,0,0,0,0,2,0,0,0,0,0,0,0,1],
  [1,0,0,0,0,0,0,0,2,0,0,0,0,0,0,1],
  [1,0,0,0,0,0,0,0,2,2,2,0,0,0,0,1],
  [1,0,0,0,0,0,0,0,2,2,2,0,0,0,0,1],
  [1,0,0,0,0,0,0,0,2,2,2,0,0,0,0,1],
  [1,0,0,0,0,0,2,2,0,0,0,0,0,0,0,1],
  [1,0,0,0,0,2,2,0,0,0,0,0,0,0,0,1],
  [1,0,0,0,2,2,0,0,0,0,0,0,0,0,0,1],
  [1,0,0,0,2,2,0,0,0,0,0,0,0,0,0,1],
  [1,1,1,1,2,2,1,1,1,1,1,1,1,1,1,1]
]

var camera: Rect = Rect(x: 0, y: 0, w: 16, h: 16)

const TILES: [Sprite] = [
  --- wall
  make_sprite(0,16),
  --- water
  make_sprite(0, 8)
]

def render_tile(sprite: Sprite, x: int, y: int) do
  render_sprite(sprite, x * TILE_SIZE, y * TILE_SIZE)
end

--- tile is already remapped to be `tile-1` to avoid account for ground tiles
def render_map_tile(tile: int, x: int, y: int) do
  render_tile(TILES[tile], x, y)
end

def draw_map() do
  for y = 0 to 16 do
    for x = 0 to 16 do
      var tile: int = MAP[y][x]
      if tile != 0 do
        render_map_tile(tile - 1, x, y)
      end
    end
  end
end

def can_walk(x: int, y: int) bool do
  ret MAP[y][x] == 0
end

------------------------------ [ other stuff ... ]

enum Scene do
  Main
  Inventory
end

var scene: Scene = Scene.Main

rec Player do
  sprite: Sprite
  pos: Vec2
end

var player: Player = Player(
  sprite: make_sprite(8, 40),
  pos: start_pos
)

const MAX_POS: int = 15

def update_player() do
  --- player movement
  if buttonp(Key.Left) && can_walk(player.pos.x - 1, player.pos.y) do
    player.pos.x -= 1
  end
  if buttonp(Key.Right) && can_walk(player.pos.x + 1, player.pos.y) do
    player.pos.x += 1
  end
  if buttonp(Key.Up) && can_walk(player.pos.x, player.pos.y - 1) do
    player.pos.y -= 1
  end
  if buttonp(Key.Down) && can_walk(player.pos.x, player.pos.y + 1) do
    player.pos.y += 1
  end
  --- scene selection
  if buttonp(Key.X) do
    scene = Scene.Inventory
  end
end

def draw_player() do
  render_tile(player.sprite, player.pos.x, player.pos.y)
end

def update_inventory() do
  if buttonp(Key.Z) do
    scene = Scene.Main
  end
end

def draw_inventory() do
  render_text("INVENTORY!", 0, 0)
end

def update() do
  if scene == Scene.Main do
    update_player()
  end
  if scene == Scene.Inventory do
    update_inventory()
  end
end

def draw() do
  clear()
  if scene == Scene.Main do
    draw_map()
    draw_player()
  end
  if scene == Scene.Inventory do
    draw_inventory()
  end
end
