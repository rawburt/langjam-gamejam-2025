use std
use textures

const TEXT_SIZE: int = 8

def text_rect(x: int, y: int) Rect do
  ret Rect(x: x, y: y, w: TEXT_SIZE, h: TEXT_SIZE)
end

def make_text_sprite(x: int, y: int) Sprite do
  var sprite: Sprite = Sprite(
    texture: TILE_TEXTURE,
    rect: text_rect(x, y),
    overlay: false,
    overlay_color: #ffffff
  )
  ret sprite
end

const CHARS: [str] = [
  "A", "B", "C", "D", "E", "F", "G", "H",
  "I", "J", "K", "L", "M", "N", "O", "P",
  "Q", "R", "S", "T", "U", "V", "W", "X",
  "Y", "Z",
  ".", ",", ":", "?", "!",
  "1", "2", "3", "4", "5", "6", "7", "8", "9", "0"
]

def char_index(char: str) int do
  var index: int = 0
  for i = 0 to len(CHARS) do
    if CHARS[i] == char do
      index = i
      break
    end
  end
  ret index
end

var char_sprites: [Sprite] = []

--- "A" - "Z"
for x = 0 to 26 do
  push(make_text_sprite(x * TILE_SIZE, 48), char_sprites)
end
--- "."
push(make_text_sprite(104, 40), char_sprites)
--- ","
push(make_text_sprite(112, 40), char_sprites)
--- ":"
push(make_text_sprite(120, 40), char_sprites)
--- "?"
push(make_text_sprite(160, 40), char_sprites)
--- "!"
push(make_text_sprite(0, 40), char_sprites)
--- numbers
for x = 0 to 10 do
  push(make_text_sprite(x * TILE_SIZE, 56), char_sprites)
end

def render_text(text: str, x: int, y: int) do
  for i = 0 to len(text) do
    var char: str = text[i]
    if char != " " do
      var sprite_index: int = char_index(char)
      var sprite: Sprite = char_sprites[sprite_index]
      render_sprite(sprite, (x * TEXT_SIZE) + i * TEXT_SIZE, y * TEXT_SIZE)
    end
  end
end

def render_text_centered(text: str, y: int) do
  var x: int = (16 - len(text)) / 2
  render_text(text, x, y)
end
